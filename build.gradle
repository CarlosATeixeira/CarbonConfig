plugins {
    id 'eclipse'
    id 'java-library'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'net.darkhax.curseforgegradle' version '1.1.15'
    id 'com.modrinth.minotaur' version '2.+'
}

apply plugin: 'java'
apply plugin: 'eclipse'
jarJar.enable()

version = MC_VERSION + '-' + MOD_VERSION
group = 'carbonconfiglib' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'CarbonConfig'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

sourceSets {
    main {
        java {
 			srcDir "carbonconfig/src/main/java"
 			srcDir "src/main/java"
 		}
    }
}

minecraft {
    mappings channel: 'official', version: '1.20'
    runs {
        client {
            workingDirectory project.file('run')
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
        gameTestServer {
            workingDirectory project.file('run')
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
    flatDir {
       dirs("libs")
   	}
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20-46.0.14'
//	implementation fg.deobf("curse.maven:the-one-probe-245211:3965693")
    minecraftLibrary ':Primitive Collections-0.8.9'
    jarJar(group: 'com.github.Speiger', name: 'Primitive Collections', version: '[0.8,0.9)') {
	 	jarJar.pin(it, "0.8.9")
	}
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "examplemod",
                "Specification-Vendor"    : "examplemodsareus",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "examplemodsareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')
reobf {
    jarJar { }
}
tasks.jarJar.finalizedBy('reobfJarJar')

task deobfJar(type: Jar) {
    archiveClassifier.set("deobf")
    from sourceSets.main.output
}

artifacts {
    archives deobfJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

task renameBuild (type: Copy) {
	dependsOn jar
	dependsOn reobf
	include "*-all.jar"
    from ('build/libs')
    into ('build/libs')
    rename { name ->  
    	System.out.println('replacing: '+name)
    	name.replace '-all', ''
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task deleteBuild(type: Delete) {
 	delete fileTree('build/libs') {
 		include '*-all.jar'
	}
}

task publishMods() {
	group 'publishing'
	description 'Publishes the mod to all Mod Distributors'
	doLast {
		System.out.println("Published Mods to Curseforge/Modrinth")
	}
}
tasks.reobfJarJar.finalizedBy('renameBuild')
renameBuild.mustRunAfter('reobfJar')
renameBuild.finalizedBy('deleteBuild')
publishMods.dependsOn('modrinth')
publishMods.dependsOn('curseforge')


modrinth {
	token = System.getenv("Modrinth_API_Token");
	projectId = 'carbon-config'
	versionName = archivesBaseName.replace(" ", "")+"-"+version
	versionNumber = MOD_VERSION
	versionType = RELEASE_TYPE
	uploadFile = jar
	gameVersions = [MC_VERSION, '1.20.1']
	loaders = ['forge']
	
    def logs = new File(projectDir, "/changelogs/"+MOD_VERSION.replace(".", "")+".md");
    changelog = logs.exists() ? logs.getText('UTF-8') : "";
}

task curseforge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) { 
	mustRunAfter assemble
	group 'publishing'
	description 'Publishes the mod to Curseforge'
	apiToken = System.getenv("Curseforge_API_Token")
		
	def mainFile = upload(898104, jar)
    mainFile.releaseType = RELEASE_TYPE
    mainFile.displayName = archivesBaseName.replace(" ", "")+"-"+version
    
    def changelog = new File(projectDir, "/changelogs/"+MOD_VERSION.replace(".", "")+".md");
    mainFile.changelog = changelog.exists() ? changelog.getText('UTF-8') : "";
    mainFile.changelogType = 'markdown'
    mainFile.addGameVersion(MC_VERSION, '1.20.1', 'Server', 'Client')
    mainFile.addJavaVersion('Java 17')
}